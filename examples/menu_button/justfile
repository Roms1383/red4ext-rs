set windows-shell := ["powershell.exe", "-NoLogo", "-Command"]
set dotenv-load
set fallback

DEFAULT_GAME_DIR   := join("C:\\", "Program Files (x86)", "Steam", "steamapps", "common", "Cyberpunk 2077")

game_dir           := env_var_or_default("GAME_DIR", DEFAULT_GAME_DIR)
red4ext_game_dir   := join(game_dir, "red4ext", "plugins")
redscript_game_dir := join(game_dir, "r6", "scripts")

mod_name           := "menu_button"
bin_name           := mod_name + ".dll"
log_name           := mod_name + ".log"

hot-reload:
  @just reload '{{justfile_directory()}}' '{{mod_name}}'

install target='release':
  @just build '{{target}}' '{{justfile_directory()}}'
  @just setup '{{ join(red4ext_game_dir, mod_name) }}'
  @just overwrite-file '{{ join(justfile_directory(), "target", target, bin_name) }}' '{{ join(red4ext_game_dir, mod_name, bin_name) }}';

uninstall:
  @just delete '{{ join(red4ext_game_dir, mod_name) }}'
  @just delete '{{ join(redscript_game_dir, capitalize(mod_name)) }}'

# display red4ext logs
logs:
  @just cat '{{ join(game_dir, "red4ext", "logs", log_name) }}'